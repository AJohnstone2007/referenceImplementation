(* adl - a dynamic language (or maybe Adrian's dynamic language) *)
!whitespace(&SIMPLE_WHITESPACE,&COMMENT_BLOCK_C,&COMMENT_LINE_C)

seq  ::= expr^^ | seq sep^ expr
 sep ::= #^ | ';'^ sep^ 
expr ::= bind^^
bind ::= list^^ | const^^ | assign^^
 const ::= lhsID '='^ expr 
 assign ::= lhsID ':='^ expr
 lhsID ::= &ID
list ::= iter^^ | iter ','^ list 
iter ::=  sel^^ | sel '@'^ iter skip | sel '@'^ iter '~'^ iter
sel  ::=   or^^ |  or '?'^ sel  skip |  or '?'^ sel  '~'^ sel 
 skip ::= #^
or   ::=  xor^^ |  or '|'^ xor 
xor  ::=  and^^ | xor '^'^ and 
and  ::=  eq0^^ | eq0 '&'^ and
eq0  ::=  rel0^^ | eq^^ | ne^^
 eq  ::= rel0 '=='^ rel0 
 ne  ::= rel0 '!='^ rel0 
rel0 ::=  cat^^ | ge^^ | gt^^ | le^^ | lt^^
 ge  ::= cat '>='^ cat 
 gt  ::= cat '>'^ cat 
 le  ::= cat '<='^ cat 
 lt  ::= cat '<'^ cat
cat  ::=  sh0^^  | cat '::'^ sh0
sh0  ::=  add0^^ | lsh^^ | rsh^^ | rol^^ | ror^^ | ash^^
 lsh ::= sh0 '<<'^ add0 
 rsh ::= sh0 '>>'^ add0 
 rol ::= sh0 '<<|'^ add0 
 ror ::= sh0 '>>|'^ add0
 ash ::= sh0 '>>>'^ add0 
add0 ::=  mul0^^ | add^^ | sub^^
 add ::= add0 '+'^ mul0 
 sub ::= add0 '-'^ mul0 
mul0 ::=  exp^^ | mul^^ | div^^ | mod^^
 mul ::= mul0 '*'^ exp  
 div ::= mul0 '/'^ exp 
 mod ::= mul0 '%'^ exp
exp  ::=   op^^ | op '**'^ exp
op   ::=  pos^^ | neg^^ | not^^ | lambda^^
| __bool^^ | __int32^^ | __real64^^ | __char^^ | __string^^ | deref^^ | '_'^^ | __done | __empty | break | continue | return | yield 
| '('^ seq^^ ')'^ | scope^^
 pos ::= '!+'^ op 
 neg ::= '!-'^ op 
 not ::= '!'^ op
 deref ::= ID^
 __done ::= 'done'^
 __empty ::= 'empty'^
 break ::= 'break'^
 continue ::= 'continue'^
 return ::= 'return'^
 yield ::= 'yield'

 lambda ::= '\\'^ list scope
 scope  ::= '{'^ list '}'^ 

ID       ::= &ID | &ID ':'^ &ID
__bool   ::= 'True'^^ | 'False'^^
__int32  ::= &INTEGER
__real64 ::= &REAL
__char   ::= &STRING_SQ
__string ::= &STRING_DQ
